
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ИменаРеквизитов = СтруктураСоответствияНастройкиОтборовРеквизитамФормы();
	ИменаРеквизитовБазыКорреспондента = СтруктураСоответствияНастройкиОтборовКорреспондентаРеквизитамФормы();

	
	РежимСинхронизацииОрганизаций = ?(ИспользоватьОтборПоОрганизациям, "СинхронизироватьДанныеТолькоПоВыбраннымОрганизациям", "СинхронизироватьДанныеПоВсемОрганизациям");
	РежимВыгрузкиСотрудников = ?(НеВыгружатьПерсональныеДанныеФизическихЛиц,1,0);
	
	ПолучитьОписаниеКонтекста();
	
	ЗначениеВыгружатьВсегда = Перечисления.РежимыВыгрузкиОбъектовОбмена.ВыгружатьВсегда;
	ЗначениеВыгружатьПриНеобходимости = Перечисления.РежимыВыгрузкиОбъектовОбмена.ВыгружатьПриНеобходимости;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	РежимСинхронизацииОрганизацийПриИзмененииЗначения();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	ОбменДаннымиКлиент.ФормаНастройкиПередЗакрытием(Отказ, ЭтаФорма, ЗавершениеРаботы);
	
КонецПроцедуры

&НаКлиенте
Процедура РежимВыгрузкиСотрудниковПриИзменении(Элемент)
	
	НеВыгружатьПерсональныеДанныеФизическихЛиц = ?(РежимВыгрузкиСотрудников = 0,Ложь, Истина);
	
	Если НеВыгружатьПерсональныеДанныеФизическихЛиц Тогда
		РежимВыгрузкиПерсональныеДанные = ЗначениеВыгружатьПриНеобходимости;
	Иначе
		РежимВыгрузкиПерсональныеДанные = ЗначениеВыгружатьВсегда;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ИспользоватьОтборПоОрганизациям =  (РежимСинхронизацииОрганизаций = "СинхронизироватьДанныеТолькоПоВыбраннымОрганизациям");
	
	Если Не ИспользоватьОтборПоОрганизациям Тогда
		Организации.Очистить();
	КонецЕсли;
	
	ПолучитьОписаниеКонтекста();
	
	ОбменДаннымиКлиент.ФормаНастройкиУзловКомандаЗакрытьФорму(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ВключитьВсеОрганизации(Команда)
	
	ВключитьОтключитьВсеЭлементыВТаблице(Истина, "ОрганизацииЭтойПрограммы");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьВсеОрганизации(Команда)
	
	ВключитьОтключитьВсеЭлементыВТаблице(Ложь, "ОрганизацииЭтойПрограммы");
	
КонецПроцедуры

&НаКлиенте
Процедура ВключитьВсеОрганизацииКорреспондента(Команда)
	
	ВключитьОтключитьВсеЭлементыВТаблице(Истина, "ОрганизацииКорреспондента");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьВсеОрганизацииКорреспондента(Команда)
	
	ВключитьОтключитьВсеЭлементыВТаблице(Ложь, "ОрганизацииКорреспондента");
	
КонецПроцедуры


&НаКлиенте
Процедура РежимСинхронизацииОрганизацийПриИзменении(Элемент)
	
	РежимСинхронизацииОрганизацийПриИзмененииЗначения();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВключитьОтключитьВсеЭлементыВТаблице(Включить, ИмяТаблицы)
	
	Для Каждого ЭлементКоллекции Из ЭтаФорма[ИмяТаблицы] Цикл
		
		ЭлементКоллекции.Использовать = Включить;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьОписаниеКонтекста()
	
	// Дата начала выгрузки документов
	// отбор по Организациям.
	Если ИспользоватьОтборПоОрганизациям Тогда
		ОрганизацииОписание = НСтр("ru='Только по организациям:';uk='Тільки за організаціями:'") + Символы.ПС + ИспользуемыеЭлементы("ОрганизацииЭтойПрограммы") + Символы.ПС + ИспользуемыеЭлементы("ОрганизацииКорреспондента");
	Иначе
		ОрганизацииОписание = НСтр("ru='По всем организациям.';uk='По всіх організаціях.'");
	КонецЕсли;
	
	Если НеВыгружатьПерсональныеДанныеФизическихЛиц Тогда
		ОграничениеВыгружатьПерсональныеДанные = НСтр("ru='Данные для формирования проводок будут выгружаться сводно.';uk='Дані для формування проводок вивантажуватимуться зведено.'");;
	Иначе
		ОграничениеВыгружатьПерсональныеДанные = НСтр("ru='Данные для формирования проводок будут выгружаться с детализацией по сотрудникам.';uk='Дані для формування проводок вивантажуватимуться з деталізацією по співробітникам.'"); 
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДатаНачалаИспользованияОбмена) Тогда
		ПредставлениеДаты = Формат(ДатаНачалаИспользованияОбмена, "ДЛФ=D");
		ОграничениеДатаНачалаИспользованияОбмена = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Программа будет автоматически отслеживать изменения документов, и регистрировать их к отправке начиная с даты: %1';uk='Програма автоматично відстежуватиме зміни документів і реєструватиме їх до відправлення починаючи з дати: %1'"),ПредставлениеДаты);
	Иначе
		ОграничениеДатаНачалаИспользованияОбмена = ""; 
	КонецЕсли;
	
	ОписаниеКонтекста = (""	+ ОрганизацииОписание
		+ Символы.ПС
		+ ОграничениеВыгружатьПерсональныеДанные
		+ Символы.ПС
		+ ОграничениеДатаНачалаИспользованияОбмена);
	
КонецПроцедуры

&НаСервере
Функция ИспользуемыеЭлементы(ИмяТаблицы)
	
	Возврат СтрСоединить(
			ЭтаФорма[ИмяТаблицы].Выгрузить(Новый Структура("Использовать", Истина)).ВыгрузитьКолонку("Представление"),
			Символы.ПС
	);
	
КонецФункции

&НаКлиенте
Процедура РежимСинхронизацииОрганизацийПриИзмененииЗначения()
	
	Элементы.ОрганизацииЭтойПрограммы.Доступность =
		(РежимСинхронизацииОрганизаций = "СинхронизироватьДанныеТолькоПоВыбраннымОрганизациям");
	Элементы.ОрганизацииКорреспондента.Доступность =
		(РежимСинхронизацииОрганизаций = "СинхронизироватьДанныеТолькоПоВыбраннымОрганизациям");	
	
	
КонецПроцедуры

&НаСервере
Функция СтруктураСоответствияНастройкиОтборовРеквизитамФормы()
	
	СтруктураНастроек = Новый Структура;
	СтруктураНастроек.Вставить("ИспользоватьОтборПоОрганизациям",         "ИспользоватьОтборПоОрганизациям");
	СтруктураНастроек.Вставить("Организации",                             "ОрганизацииЭтойПрограммы");
	
	Возврат СтруктураНастроек;
	
КонецФункции

&НаСервере
Функция СтруктураСоответствияНастройкиОтборовКорреспондентаРеквизитамФормы()
	
	СтруктураНастроек = Новый Структура;
	СтруктураНастроек.Вставить("ИспользоватьОтборПоОрганизациям", "ИспользоватьОтборПоОрганизациям");
	СтруктураНастроек.Вставить("Организации",                     "ОрганизацииКорреспондента");
	
	Возврат СтруктураНастроек;
	
КонецФункции


#КонецОбласти
