#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти  

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ПриказПокупкаОС";
	КомандаПечати.Представление = НСтр("ru='Печать приказа на покупку ОС';uk='Друк наказу на купівлю ОЗ'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.СписокФорм    = "ФормаСписка,ФормаДокумента";
	
	// Реестр документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор  = "Реестр";
	КомандаПечати.Представление  = НСтр("ru='Реестр документов';uk='Реєстр документів'");
	КомандаПечати.ЗаголовокФормы = НСтр("ru='Реестр документов ""Приказ на покупку ОС""';uk='Реєстр документів ""Наказ на купівлю ОЗ""'");
	КомандаПечати.Обработчик     = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
	КомандаПечати.СписокФорм     = "ФормаСписка";
	КомандаПечати.Порядок        = 100;

КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
		
	// Устанавливаем признак доступности печати покомплектно.
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;

	// Проверяем, нужно ли для макета ОтчетККМ формировать табличный документ.
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПриказПокупкаОС") Тогда
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		ИмяМакета = "";
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
			"ПриказПокупкаОС",
			НСтр("ru='Печать приказа на покупку ОС';uk='Друк наказу про купівлю ОЗ'"),
			ПолучитьТабличныйДокумент_Приказ(МассивОбъектов, ОбъектыПечати, ИмяМакета),
			,
			ИмяМакета);
	КонецЕсли;
		
		
КонецПроцедуры

Функция ПолучитьТабличныйДокумент_Приказ(МассивОбъектов, ОбъектыПечати, ИмяМакета)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_КомлпектВодителю";
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	Макет = ПолучитьМакет("МакетПриказа");

	Для Каждого Док Из МассивОбъектов Цикл
		СтруктураПараметров = ПолучитьСтруктуру(Док);
		
		Область = Макет.ПолучитьОбласть("Шапка");
		Область.Параметры.Заполнить(СтруктураПараметров);
		ТабличныйДокумент.Вывести(Область);
		
		Область = Макет.ПолучитьОбласть("Строка");
		Номер = 1;
		Для Каждого Стр Из Док.Оборудование Цикл
			Область.Параметры.Номер = СтрШаблон("1.%1", Номер);
			Область.Параметры.Номенклатура = Стр.Номенклатура.НаименованиеПолное;
			Область.Параметры.Кво = Стр.Количество;
			Область.Параметры.КвоПрописью = ЧислоПрописью(Стр.Количество,
				"Л = "+Локализация.ОпределитьКодЯзыкаДляФормат("uk"),
				",,,,,,,,0"
			);
			Область.Параметры.ЕдИзм = Стр.ЕдиницаИзмерения.Наименование;
			ТабличныйДокумент.Вывести(Область);
			Номер = Номер + 1;
		КонецЦикла;
		
		Если Не ПустаяСтрока(Док.ДополнительныеСведения) Тогда
			Область = Макет.ПолучитьОбласть("ДопСведения");
			Область.Параметры.Заполнить(СтруктураПараметров);
			ТабличныйДокумент.Вывести(Область);
		КонецЕсли;
		
		Область = Макет.ПолучитьОбласть("Подвал");
		Область.Параметры.Заполнить(СтруктураПараметров);
		ТабличныйДокумент.Вывести(Область);
		
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();	
	КонецЦикла;
	
	Возврат ТабличныйДокумент;  
	
КонецФункции

Функция ПолучитьСтруктуру(Док)
	
	Результат = Новый Структура;
	Результат.Вставить("ОрганизацияПолноеНаименование", Док.Организация.НаименованиеПолное);
	Результат.Вставить("ИНН", Док.Организация.КодПоЕДРПОУ);
	Результат.Вставить("НомерПриказа", ?(ЗначениеЗаполнено(Док.НомерПриказа), СокрЛП(Док.НомерПриказа), ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Док.Номер)));
	Результат.Вставить("ДатаПриказа", Формат(Док.Дата,"Л=uk_UA; ДФ='dd MMMM yyyy р.'"));
    Результат.Вставить("ДополнительныеСведения", СокрЛП(Док.ДополнительныеСведения));
	ОтвЛица = ОтветственныеЛицаБП.ОтветственныеЛица(Док.Организация, Док.Дата);
	Результат.Вставить("Должность", ОтвЛица.РуководительДолжностьПредставление);
	Результат.Вставить("ФИО", ОтвЛица.РуководительПредставление);
	
	Возврат Результат;
	
КонецФункции

