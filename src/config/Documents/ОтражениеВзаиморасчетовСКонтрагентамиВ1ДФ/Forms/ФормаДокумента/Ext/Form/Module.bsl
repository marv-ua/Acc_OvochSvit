#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	// Обработчик подсистемы "ВерсионированиеОбъектов"
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	
	Если Параметры.Ключ.Пустая() Тогда
		
		// создается новый документ
		ЗначенияДляЗаполнения = Новый Структура("ПредыдущийКвартал, Организация, Ответственный",
		"Объект.ПериодРегистрации",
		"Объект.Организация",
		"Объект.Ответственный");
		
		ЗарплатаКадры.ЗаполнитьПервоначальныеЗначенияВФорме(ЭтаФорма, ЗначенияДляЗаполнения);
		
	КонецЕсли;
	
	ПериодНачисленияСтрокой = Формат(Объект.ПериодРегистрации, "ДФ='q ''квартал'' yyyy'");
	ПериодНачисленияСтрокойМесяц = Формат(Объект.ПериодРегистрации, "Л="+ЛокализацияПовтИсп.КодЯзыкаИнтерфейса()+";ДФ='ММММ гггг'");
			
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры	

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОбработатьИзменениеОрганизацииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодНачисленияСтрокойПриИзменении(Элемент)
	
	ПериодНачисленияСтрокой = Формат(ЭтотОбъект.Объект.ПериодРегистрации, "ДФ='q ''квартал'' yyyy'");
	ПериодНачисленияСтрокойМесяц = Формат(Объект.ПериодРегистрации, "Л="+ЛокализацияПовтИсп.КодЯзыкаИнтерфейса()+";ДФ='ММММ гггг'");
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодНачисленияСтрокойНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормыВыбора = Новый Структура("НачалоПериода, КонецПериода, ВидПериода",
		НачалоКвартала(ЭтотОбъект.Объект.ПериодРегистрации),
		КонецКвартала(ЭтотОбъект.Объект.ПериодРегистрации),
		ПредопределенноеЗначение("Перечисление.ДоступныеПериодыОтчета.Квартал"));
		
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПериодЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериодаКвартал", ПараметрыФормыВыбора, Элементы.ПериодНачисленияСтрокой, , , , ОписаниеОповещения);

КонецПроцедуры

&НаКлиенте
Процедура ПериодНачисленияСтрокойОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

КонецПроцедуры

&НаКлиенте
Процедура ПериодНачисленияСтрокойМесяцНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормыВыбора = Новый Структура("НачалоПериода, КонецПериода, ВидПериода",
		НачалоМесяца(ЭтотОбъект.Объект.ПериодРегистрации),
		КонецМесяца(ЭтотОбъект.Объект.ПериодРегистрации),
		ПредопределенноеЗначение("Перечисление.ДоступныеПериодыОтчета.Месяц"));
		
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПериодЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериодаМесяц", ПараметрыФормыВыбора, Элементы.ПериодНачисленияСтрокой, , , , ОписаниеОповещения);

КонецПроцедуры

&НаКлиенте
Процедура ПериодНачисленияСтрокойМесяцОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования,
		ЭтотОбъект,
		"Объект.Комментарий"
	);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьПоВзаиморасчетам(Команда)
		
	Если Объект.Контрагенты.Количество() > 0 
		Или Объект.НДФЛ.Количество() > 0 Тогда
		ТекстВопроса = НСтр("ru='Табличные части будут очищены.
|Продолжить?';uk='Табличні частини будуть очищені.
|Продовжити?'");
		
		Оповещение = Новый ОписаниеОповещения("ВопросЗаполнитьПоВзаиморасчетам", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе
		ЗаполнитьПоВзаиморасчетамСервер(Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросЗаполнитьПоОплате(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗаполнитьПоВзаиморасчетамСервер(Ложь);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВопросЗаполнитьПоВзаиморасчетам(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗаполнитьПоВзаиморасчетамСервер(Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоВзаиморасчетамСервер(ПоВзаиморасчетам)
	
		
	Объект.Контрагенты.Очистить(); 
	Объект.НДФЛ.Очистить();

	Запрос = Новый Запрос;
	
	Если НЕ ПоВзаиморасчетам Тогда
		ТекстЗапроса = "                                                                                        
		|ВЫБРАТЬ
		|	ОстакиОбороты.Контрагент	КАК Контрагент,
		|	СУММА(ОстакиОбороты.УдержНДФЛ)  КАК УдержНДФЛ,
		|	СУММА(ОстакиОбороты.УдержВС)  КАК УдержВС,
		|	СУММА(ВЫБОР	КОГДА (ОстакиОбороты.ОбДт > ОстакиОбороты.ОбКт) ТОГДА ОстакиОбороты.ОбДт - ОстакиОбороты.ОбКт ИНАЧЕ 0 КОНЕЦ
		|		)  КАК НачисленоДохода
		|	
		|	ИЗ(
		|		ВЫБРАТЬ
		|			Хозрасчетный.Контрагент	КАК Контрагент,
		|			Хозрасчетный.Договор	КАК Договор,
		|			СУММА(Хозрасчетный.УдержНДФЛ)  КАК УдержНДФЛ,
		|			СУММА(Хозрасчетный.УдержВС)  КАК УдержВС,
		|			СУММА(Хозрасчетный.ОбКт)  КАК ОбКт,
		|			СУММА(Хозрасчетный.ОбДт)  КАК ОбДт
		|		ИЗ (
		|				ВЫБРАТЬ
		|					ХозрасчетныйОбороты.Субконто1	КАК Контрагент,
		|					ХозрасчетныйОбороты.Субконто2	КАК Договор,
		|					ВЫБОР КОГДА ХозрасчетныйОбороты.КорСчет в (&Счет6411)
		|	               			ТОГДА ХозрасчетныйОбороты.СуммаОборотДт
		|	               			ИНАЧЕ 0 КОНЕЦ                         КАК УдержНДФЛ,
		|					ВЫБОР КОГДА ХозрасчетныйОбороты.КорСчет в (&Счет642)
		|	               			ТОГДА ХозрасчетныйОбороты.СуммаОборотДт
		|	               			ИНАЧЕ 0 КОНЕЦ                         КАК УдержВС,
		|					ХозрасчетныйОбороты.СуммаОборотДт  КАК ОбДт,
		|					ХозрасчетныйОбороты.СуммаОборотКт  КАК ОбКт
		|					
		|				ИЗ РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, ДЕНЬ, Счет В ИЕРАРХИИ (&СписокСчетов), , Организация = &парамОрганизация, , ) КАК ХозрасчетныйОбороты
		|
		|				ГДЕ ХозрасчетныйОбороты.Субконто1.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
		|					И (ХозрасчетныйОбороты.КорСчет В ИЕРАРХИИ (&СписокКорСчетов) или ХозрасчетныйОбороты.КорСчет в (&Счет6411) или ХозрасчетныйОбороты.КорСчет в (&Счет642))
		|		  ) КАК Хозрасчетный
		|
		|		СГРУППИРОВАТЬ ПО 
		|			Хозрасчетный.Контрагент,
		|			Хозрасчетный.Договор
		|	) КАК ОстакиОбороты	
		|
		|ГДЕ
		|	ОстакиОбороты.обДт > 0 ИЛИ ОстакиОбороты.ОбКт > 0
		|
		|СГРУППИРОВАТЬ ПО 
		|	ОстакиОбороты.Контрагент    
		|   
		|";
	Иначе
		// по оплате
		ТекстЗапроса = "                                                                                        
		|ВЫБРАТЬ
		|	ОстакиОбороты.Контрагент	КАК Контрагент,
		|	СУММА(ОстакиОбороты.УдержНДФЛ)  КАК УдержНДФЛ,
		|	СУММА(ОстакиОбороты.УдержВС)  КАК УдержВС,
		|	СУММА(ВЫБОР	КОГДА (ОстакиОбороты.ОбКт -ОстакиОбороты.ОстДт + ВЫБОР	КОГДА ОстакиОбороты.Возврат < 0 ТОГДА ОстакиОбороты.Возврат ИНАЧЕ 0 КОНЕЦ )
		|	> (ОстакиОбороты.обДт - ОстакиОбороты.ОстКт - ВЫБОР	КОГДА ОстакиОбороты.Возврат > 0 ТОГДА ОстакиОбороты.Возврат ИНАЧЕ 0 КОНЕЦ)
		|			ТОГДА (ОстакиОбороты.ОбКт-ОстакиОбороты.ОстДт  + ВЫБОР	КОГДА ОстакиОбороты.Возврат < 0 ТОГДА ОстакиОбороты.Возврат ИНАЧЕ 0 КОНЕЦ)  // отнимаем возвраты товаром
		|			ИНАЧЕ (ОстакиОбороты.обДт -ОстакиОбороты.ОстКт - ВЫБОР	КОГДА ОстакиОбороты.Возврат > 0 ТОГДА ОстакиОбороты.Возврат ИНАЧЕ 0 КОНЕЦ)  // отнимаем возвраты деньгами  
		|		КОНЕЦ)  КАК НачисленоДохода
		|	
		|	ИЗ(
		
		|		ВЫБРАТЬ
		|			Хозрасчетный.Контрагент	КАК Контрагент,
		|			Хозрасчетный.Договор	КАК Договор,
		|			СУММА(Хозрасчетный.УдержНДФЛ)  КАК УдержНДФЛ,
		|			СУММА(Хозрасчетный.УдержВС)  КАК УдержВС,
		|			СУММА(Хозрасчетный.ОстКт)  КАК ОстКт,
		|			СУММА(Хозрасчетный.ОстДт)  КАК ОстДт,
		|			СУММА(Хозрасчетный.ОбКт)  КАК ОбКт,
		|			СУММА(Хозрасчетный.ОбДт)  КАК ОбДт,
		|			СУММА(Хозрасчетный.Возврат)  КАК Возврат
		|		ИЗ (
		|				ВЫБРАТЬ
		|					ХозрасчетныйОбороты.Субконто1	КАК Контрагент,
		|					ХозрасчетныйОбороты.Субконто2	КАК Договор,
		|	               	0                         		КАК УдержНДФЛ,
		|	               	0                         		КАК УдержВС,
		|					ХозрасчетныйОбороты.СуммаОстатокДт  КАК ОстДт,
		|					ХозрасчетныйОбороты.СуммаОстатокКт  КАК ОстКт,
		|					0  КАК ОбДт,
		|					0  КАК ОбКт,
		|					0  КАК Возврат
		|					
		|				ИЗ РегистрБухгалтерии.Хозрасчетный.Остатки(&НачалоПериода, Счет В ИЕРАРХИИ (&СписокСчетов), , Организация = &парамОрганизация) КАК ХозрасчетныйОбороты
		|				ГДЕ ХозрасчетныйОбороты.Субконто1.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
		|               //авансы
		|				ОБЪЕДИНИТЬ ВСЕ
		|				ВЫБРАТЬ
		|					ХозрасчетныйОбороты.Субконто1	КАК Контрагент,
		|					ХозрасчетныйОбороты.Субконто2	КАК Договор,
		|					ВЫБОР КОГДА ХозрасчетныйОбороты.КорСчет в (&Счет6411)
		|	               			ТОГДА ХозрасчетныйОбороты.СуммаОборотДт
		|	               			ИНАЧЕ 0 КОНЕЦ                         КАК УдержНДФЛ,
		|					ВЫБОР КОГДА ХозрасчетныйОбороты.КорСчет в (&Счет642)
		|	               			ТОГДА ХозрасчетныйОбороты.СуммаОборотДт
		|	               			ИНАЧЕ 0 КОНЕЦ                         КАК УдержВС,
		|					0  КАК ОстДт,
		|					0  КАК ОстКт,
		|					ХозрасчетныйОбороты.СуммаОборотДт  КАК ОбДт,
		|					0  КАК ОбКт,
		|					0  КАК Возврат
		|					
		|				ИЗ РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, ДЕНЬ, Счет В ИЕРАРХИИ (&СписокСчетовАвансы), , Организация = &парамОрганизация, , ) КАК ХозрасчетныйОбороты
		|
		|				ГДЕ ХозрасчетныйОбороты.Субконто1.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
		|               //возврат аванса
		|				ОБЪЕДИНИТЬ ВСЕ
		|				ВЫБРАТЬ
		|					ХозрасчетныйОбороты.Субконто1	КАК Контрагент,
		|					ХозрасчетныйОбороты.Субконто2	КАК Договор,
		|					0  КАК УдержНДФЛ,
		|					0  КАК УдержВС,
		|					0  КАК ОстДт,
		|					0  КАК ОстКт,
		|					0  КАК ОбДт,
		|					0  КАК ОбКт,
		|					ХозрасчетныйОбороты.СуммаОборотКт  КАК Возврат
		|					
		|				ИЗ РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, ДЕНЬ, Счет В ИЕРАРХИИ (&СписокСчетовАвансы) ИЛИ Счет В ИЕРАРХИИ (&СписокСчетовПоставщики), , Организация = &парамОрганизация, , ) КАК ХозрасчетныйОбороты
		|
		|				ГДЕ ХозрасчетныйОбороты.Субконто1.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
		|                  И ХозрасчетныйОбороты.КорСчет в иерархии (&СписокКорСчетов)
		|               //Поставка
		|				ОБЪЕДИНИТЬ ВСЕ
		|				ВЫБРАТЬ
		|					ХозрасчетныйОбороты.Субконто1	КАК Контрагент,
		|					ХозрасчетныйОбороты.Субконто2	КАК Договор,
		|					ВЫБОР КОГДА ХозрасчетныйОбороты.КорСчет в (&Счет6411)
		|	               			ТОГДА ХозрасчетныйОбороты.СуммаОборотДт
		|	               			ИНАЧЕ 0 КОНЕЦ                         КАК УдержНДФЛ,
		|					ВЫБОР КОГДА ХозрасчетныйОбороты.КорСчет в (&Счет642)
		|	               			ТОГДА ХозрасчетныйОбороты.СуммаОборотДт
		|	               			ИНАЧЕ 0 КОНЕЦ                         КАК УдержВС,
		|					0  КАК ОстДт,
		|					0  КАК ОстКт,
		|					0  КАК ОбДт,
		|					ХозрасчетныйОбороты.СуммаОборотКт  КАК ОбКт,
		|					0  КАК Возврат
		|					
		|				ИЗ РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, ДЕНЬ, Счет В ИЕРАРХИИ (&СписокСчетовПоставщики), , Организация = &парамОрганизация, , ) КАК ХозрасчетныйОбороты
		|
		|				ГДЕ ХозрасчетныйОбороты.Субконто1.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
		|				  И ХозрасчетныйОбороты.КорСчет НЕ в (&СписокСчетовАвансы)
		|				  И ХозрасчетныйОбороты.КорСчет НЕ в (&СписокСчетовПоставщики)
		|				  И ХозрасчетныйОбороты.КорСчет НЕ в иерархии (&СписокКорСчетов)
		|               //оплата
		|				ОБЪЕДИНИТЬ ВСЕ
		|				ВЫБРАТЬ
		|					ХозрасчетныйОбороты.Субконто1	КАК Контрагент,
		|					ХозрасчетныйОбороты.Субконто2	КАК Договор,
		|					ВЫБОР КОГДА ХозрасчетныйОбороты.КорСчет в (&Счет6411)
		|	               			ТОГДА ХозрасчетныйОбороты.СуммаОборотДт
		|	               			ИНАЧЕ 0 КОНЕЦ                         КАК УдержНДФЛ,
		|					ВЫБОР КОГДА ХозрасчетныйОбороты.КорСчет в (&Счет642)
		|	               			ТОГДА ХозрасчетныйОбороты.СуммаОборотДт
		|	               			ИНАЧЕ 0 КОНЕЦ                         КАК УдержВС,
		|					0  КАК ОстДт,
		|					0  КАК ОстКт,
		|					ХозрасчетныйОбороты.СуммаОборотДт  КАК ОбДт,
		|					0  КАК ОбКт,
		|					0  КАК Возврат
		|					
		|				ИЗ РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, ДЕНЬ, Счет В ИЕРАРХИИ (&СписокСчетовПоставщики), , Организация = &парамОрганизация, , ) КАК ХозрасчетныйОбороты
		|
		|				ГДЕ ХозрасчетныйОбороты.Субконто1.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
		|				  И ХозрасчетныйОбороты.КорСчет  в иерархии (&СписокКорСчетов)
		|               //возврат поставки
		|				ОБЪЕДИНИТЬ ВСЕ
		|				ВЫБРАТЬ
		|					ХозрасчетныйОбороты.Субконто1	КАК Контрагент,
		|					ХозрасчетныйОбороты.Субконто2	КАК Договор,
		|					0  КАК УдержНДФЛ,
		|					0  КАК УдержВС,
		|					0  КАК ОстДт,
		|					0  КАК ОстКт,
		|					0  КАК ОбДт,
		|					0  КАК ОбКт,
		|					-ХозрасчетныйОбороты.СуммаОборотДт КАК Возврат
		|					
		|				ИЗ РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, ДЕНЬ, Счет В ИЕРАРХИИ (&СписокСчетовПоставщики), , Организация = &парамОрганизация, , ) КАК ХозрасчетныйОбороты
		|
		|				ГДЕ ХозрасчетныйОбороты.Субконто1.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
		|				  И (ХозрасчетныйОбороты.КорСчет не в  (&СписокСчетовАвансы))
		|				  И (ХозрасчетныйОбороты.КорСчет не в иерархии (&СписокКорСчетов))
		|				  И (ХозрасчетныйОбороты.КорСчет не в (&СписокСчетовПоставщики))
		|				  И (ХозрасчетныйОбороты.КорСчет не в (&Счет6411))
		|				  И (ХозрасчетныйОбороты.КорСчет не в (&Счет642))
		|		  ) КАК Хозрасчетный
		|
		|		СГРУППИРОВАТЬ ПО 
		|			Хозрасчетный.Контрагент,
		|			Хозрасчетный.Договор	
		|	) КАК ОстакиОбороты	
		|
		|ГДЕ
		|	(ВЫБОР	КОГДА (ОстакиОбороты.ОбКт-ОстакиОбороты.ОстДт) > (ОстакиОбороты.обДт -ОстакиОбороты.ОстКт)
		|			ТОГДА (ОстакиОбороты.ОбКт-ОстакиОбороты.ОстДт)
		|			ИНАЧЕ (ОстакиОбороты.обДт -ОстакиОбороты.ОстКт)
		|		КОНЕЦ) > 0
		|
		|СГРУППИРОВАТЬ ПО 
		|	ОстакиОбороты.Контрагент    
		|   
		|";
    КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	                                
	Запрос.УстановитьПараметр("парамОрганизация", Объект.Организация);
	Если Объект.ПериодРегистрации < УчетНДФЛ.ДатаИзмененияСхемыУчетаВС() Тогда 
		Запрос.УстановитьПараметр("НачалоПериода", НачалоКвартала(Объект.ПериодРегистрации));
		Запрос.УстановитьПараметр("КонецПериода", КонецКвартала(Объект.ПериодРегистрации) );
	Иначе
		Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Объект.ПериодРегистрации));
		Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Объект.ПериодРегистрации) );
	КонецЕсли;	
		
	СписокСчетов = Новый СписокЗначений;
	СписокСчетов.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСОтечественнымиПоставщиками);				//631
	СписокСчетов.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПоставщикамиУчастникамиПФГ);				//633
	СписокСчетов.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСДругимиКредиторамиВНациональнойВалюте);	//6851
	СписокСчетов.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоВыданнымАвансамВНациональнойВалюте);	//3711
	
	СписокСчетовБанкКасса = Новый СписокЗначений;
	СписокСчетовБанкКасса.Добавить(ПланыСчетов.Хозрасчетный.Касса);			//301
	СписокСчетовБанкКасса.Добавить(ПланыСчетов.Хозрасчетный.СчетаВБанках);	//311
	СписокСчетовБанкКасса.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицамиВНациональнойВалюте);	//3721
	
	СписокСчетовАвансы = Новый СписокЗначений;
	СписокСчетовАвансы.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоВыданнымАвансамВНациональнойВалюте);	//3711
	
	СписокСчетовПоставщики = Новый СписокЗначений;
	СписокСчетовПоставщики.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСОтечественнымиПоставщиками);				//631
	СписокСчетовПоставщики.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПоставщикамиУчастникамиПФГ);				//633
	СписокСчетовПоставщики.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСДругимиКредиторамиВНациональнойВалюте);	//6851
	
	Запрос.УстановитьПараметр("СписокСчетов"	, СписокСчетов);
	Запрос.УстановитьПараметр("Счет6411"		, ПланыСчетов.Хозрасчетный.РасчетыПоНДФЛ);
	Запрос.УстановитьПараметр("Счет642"			, ПланыСчетов.Хозрасчетный.РасчетыПоОбязательнымПлатежам);
	Запрос.УстановитьПараметр("СписокКорСчетов"	, СписокСчетовБанкКасса);
	Запрос.УстановитьПараметр("СписокСчетовАвансы"	, СписокСчетовАвансы);
    Запрос.УстановитьПараметр("СписокСчетовПоставщики"	, СписокСчетовПоставщики);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ИНН = Выборка.Контрагент.КодПоЕДРПОУ;
		Если ИНН = Неопределено Тогда
			
			Сообщить (НСтр("ru='Внимание! Для контрагента - ЧП  ';uk='Увага! Для контрагента - ПП  '") + Выборка.Контрагент + НСтр("ru=' не заполнен идентификационный номер (реквизит ДРФО в справочнике Контрагенты). Информация о нем не включается в ведомость!';uk=' не заповнений ідентифікаційний номер (реквізит ДРФО в довіднику Контрагенти). Інформація про нього не включається у відомість!'"));
			
		КонецЕсли;
		
		ТекСтрока = Объект.НДФЛ.Добавить();
			
		ТекСтрока.Контрагент = Выборка.Контрагент;
		ТекСтрока.ВидДохода = Справочники.ВидыДоходовНДФЛ.Код42;
			
		ТекСтрока.Доход = Выборка.НачисленоДохода;
		ТекСтрока.Налог = Выборка.УдержНДФЛ;
		
		Если ЗначениеЗаполнено(Выборка.УдержВС)  Тогда
			ТекСтрокаВС = Объект.НДФЛ.Добавить();
			ТекСтрокаВС.Контрагент = Выборка.Контрагент;
			ТекСтрокаВС.Доход = Выборка.НачисленоДохода;
			ТекСтрокаВС.Налог = Выборка.УдержВС;
		Если Объект.ПериодРегистрации >= УчетНДФЛ.ДатаИзмененияСхемыУчетаВС() Тогда	
			ТекСтрокаВС.ВидДохода = Справочники.ВидыДоходовНДФЛ.Код42.ОблагаетсяВоеннымСбором2021;
		Иначе
			ТекСтрокаВС.ВидДохода = Справочники.ВидыДоходовНДФЛ.ВоенныйСбор;
		КонецЕсли;	
		КонецЕсли;
		
	КонецЦикла; 
	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоОплате(Команда)
	
		
	Если Объект.Контрагенты.Количество() > 0 
		Или Объект.НДФЛ.Количество() > 0 Тогда
		ТекстВопроса = НСтр("ru='Табличные части будут очищены.
|Продолжить?';uk='Табличні частини будуть очищені.
|Продовжити?'");
		
		Оповещение = Новый ОписаниеОповещения("ВопросЗаполнитьПоОплате", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе
		ЗаполнитьПоВзаиморасчетамСервер(Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериодЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЭтотОбъект.Объект.ПериодРегистрации = РезультатВыбора.НачалоПериода;
	
	ПериодНачисленияСтрокой = Формат(ЭтотОбъект.Объект.ПериодРегистрации, "ДФ='q ''квартал'' yyyy'");
	ПериодНачисленияСтрокойМесяц = Формат(ЭтотОбъект.Объект.ПериодРегистрации, "Л="+ЛокализацияПовтИсп.КодЯзыкаИнтерфейса()+";ДФ='ММММ гггг'");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОжиданияПериодРегистрацииПриИзменении()

	ОбработатьИзменениеПериодРегистрацииНаСервере();

КонецПроцедуры

&НаСервере
Функция ЕстьЗаполненныеТабличныеЧасти()
	
	ДанныеВТЧЕсть = Ложь;
	
	СписокТабличныхЧастей = СписокТабличныхЧастейДокумента();
	
	Для каждого ИмяТабличнойЧасти Из СписокТабличныхЧастей Цикл
		Если Объект[ИмяТабличнойЧасти].Количество() > 0 Тогда
			ДанныеВТЧЕсть = Истина;
			Прервать;
		КонецЕсли; 
	КонецЦикла;
	
	Возврат ДанныеВТЧЕсть;
	
КонецФункции

&НаСервере
Функция СписокТабличныхЧастейДокумента()
	
	СписокТабличныхЧастей = Новый Массив;
	
	СписокТабличныхЧастей.Добавить("Контрагенты");
	СписокТабличныхЧастей.Добавить("НДФЛ");
	
	Возврат СписокТабличныхЧастей;
	
КонецФункции

&НаСервере
Процедура ОбработатьИзменениеПериодРегистрацииНаСервере()

	Если ЕстьЗаполненныеТабличныеЧасти() Тогда
		ОчиститьТабличныеЧасти();
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьТабличныеЧасти()
	
	СписокТабличныхЧастей = СписокТабличныхЧастейДокумента();
	
	Для каждого ИмяТабличнойЧасти Из СписокТабличныхЧастей Цикл
		Объект[ИмяТабличнойЧасти].Очистить();
	КонецЦикла;
	

КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеОрганизацииНаСервере()

	ОчиститьТабличныеЧасти();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ БСП

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры


&НаКлиенте
Процедура ЗаполнитьПоПоступлениям(Команда)
	Если Объект.Контрагенты.Количество() > 0 
		Или Объект.НДФЛ.Количество() > 0 Тогда
		ТекстВопроса = НСтр("ru='Табличные части будут очищены.
|Продолжить?';uk='Табличні частини будуть очищені.
|Продовжити?'");
		
		Оповещение = Новый ОписаниеОповещения("ВопросЗаполнитьПоПоступлениям", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе
		ЗаполнитьПоПоступлениямСервер();
	КонецЕсли;
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура ВопросЗаполнитьПоПоступлениям(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗаполнитьПоПоступлениямСервер();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоПоступлениямСервер()
	
		
	Объект.Контрагенты.Очистить(); 
	Объект.НДФЛ.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПоступлениеТоваровУслугУслуги.Ссылка.Контрагент КАК Контрагент,
		|	СУММА(ПоступлениеТоваровУслугУслуги.Сумма + ПоступлениеТоваровУслугУслуги.СуммаНДС) КАК Доход,
		|	СУММА((ПоступлениеТоваровУслугУслуги.Сумма + ПоступлениеТоваровУслугУслуги.СуммаНДС) * 0.195) КАК УдержНДФЛ
		|ПОМЕСТИТЬ ВТ_Док
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг.Услуги КАК ПоступлениеТоваровУслугУслуги
		|ГДЕ
		|	ПоступлениеТоваровУслугУслуги.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ПоступлениеТоваровУслугУслуги.Ссылка.Проведен
		|	И ПоступлениеТоваровУслугУслуги.Ссылка.Организация = &парамОрганизация
		|	И ПоступлениеТоваровУслугУслуги.Ссылка.Контрагент.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
		|
		|СГРУППИРОВАТЬ ПО
		|	ПоступлениеТоваровУслугУслуги.Ссылка.Контрагент
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПоступлениеТоваровУслугТовары.Ссылка.Контрагент,
		|	СУММА(ПоступлениеТоваровУслугТовары.Сумма + ПоступлениеТоваровУслугТовары.СуммаНДС),
		|	СУММА((ПоступлениеТоваровУслугТовары.Сумма + ПоступлениеТоваровУслугТовары.СуммаНДС) * 0.195)
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
		|ГДЕ
		|	ПоступлениеТоваровУслугТовары.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ПоступлениеТоваровУслугТовары.Ссылка.Проведен
		|	И ПоступлениеТоваровУслугТовары.Ссылка.Организация = &парамОрганизация
		|	И ПоступлениеТоваровУслугТовары.Ссылка.Контрагент.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
		|
		|СГРУППИРОВАТЬ ПО
		|	ПоступлениеТоваровУслугТовары.Ссылка.Контрагент
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПоступлениеТоваровУслугОборудование.Ссылка.Контрагент,
		|	СУММА(ПоступлениеТоваровУслугОборудование.Сумма + ПоступлениеТоваровУслугОборудование.СуммаНДС),
		|	СУММА((ПоступлениеТоваровУслугОборудование.Сумма + ПоступлениеТоваровУслугОборудование.СуммаНДС) * 0.195)
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг.Оборудование КАК ПоступлениеТоваровУслугОборудование
		|ГДЕ
		|	ПоступлениеТоваровУслугОборудование.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ПоступлениеТоваровУслугОборудование.Ссылка.Проведен
		|	И ПоступлениеТоваровУслугОборудование.Ссылка.Организация = &парамОрганизация
		|	И ПоступлениеТоваровУслугОборудование.Ссылка.Контрагент.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
		|
		|СГРУППИРОВАТЬ ПО
		|	ПоступлениеТоваровУслугОборудование.Ссылка.Контрагент
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Док.Контрагент КАК Контрагент,
		|	СУММА(ВТ_Док.Доход) КАК Доход,
		|	СУММА(ВТ_Док.УдержНДФЛ) КАК УдержНДФЛ,
		|	ВТ_Док.Контрагент.КодПДФО КАК КонтрагентКодПДФО,
		|	ВТ_Док.Контрагент.КодВоенныеСбор КАК КонтрагентКодВоенныеСбор
		|ИЗ
		|	ВТ_Док КАК ВТ_Док
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Док.Контрагент,
		|	ВТ_Док.Контрагент.КодПДФО,
		|	ВТ_Док.Контрагент.КодВоенныеСбор";
	

	                                
	Запрос.УстановитьПараметр("парамОрганизация", Объект.Организация);
	Если Объект.ПериодРегистрации < УчетНДФЛ.ДатаИзмененияСхемыУчетаВС() Тогда 
		Запрос.УстановитьПараметр("НачалоПериода", НачалоКвартала(Объект.ПериодРегистрации));
		Запрос.УстановитьПараметр("КонецПериода", КонецКвартала(Объект.ПериодРегистрации) );
	Иначе
		Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Объект.ПериодРегистрации));
		Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Объект.ПериодРегистрации) );
	КонецЕсли;	
	
	
			
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ИНН = Выборка.Контрагент.КодПоЕДРПОУ;
		Если ИНН = Неопределено Тогда
			
			Сообщить (НСтр("ru='Внимание! Для контрагента - ЧП  ';uk='Увага! Для контрагента - ПП  '") + Выборка.Контрагент + НСтр("ru=' не заполнен идентификационный номер (реквизит ДРФО в справочнике Контрагенты). Информация о нем не включается в ведомость!';uk=' не заповнений ідентифікаційний номер (реквізит ДРФО в довіднику Контрагенти). Інформація про нього не включається у відомість!'"));
			
		КонецЕсли;
		
		ТекСтрока = Объект.НДФЛ.Добавить();
			
		ТекСтрока.Контрагент = Выборка.Контрагент;
		
		Если Выборка.КонтрагентКодПДФО = Справочники.ВидыДоходовНДФЛ.Код42 Или Не ЗначениеЗаполнено(Выборка.КонтрагентКодПДФО) Тогда
			ТекСтрока.ВидДохода = Справочники.ВидыДоходовНДФЛ.Код42;
			
			ТекСтрока.Доход = Выборка.Доход;
			//ТекСтрока.Налог = Выборка.УдержНДФЛ;
		Иначе
			ТекСтрока.ВидДохода = Выборка.КонтрагентКодПДФО;
			
			ТекСтрока.Доход = Выборка.Доход;
			ТекСтрока.Налог = Выборка.Доход*0.18;
			
			ТекСтрока = Объект.НДФЛ.Добавить();
			ТекСтрока.ВидДохода = Выборка.КонтрагентКодВоенныеСбор;
			ТекСтрока.Контрагент = Выборка.Контрагент;
			ТекСтрока.Доход = Выборка.Доход;
			ТекСтрока.Налог = Выборка.Доход*0.015;
		КонецЕсли;
		
		//Если ЗначениеЗаполнено(Выборка.УдержВС)  Тогда
		//	ТекСтрокаВС = Объект.НДФЛ.Добавить();
		//	ТекСтрокаВС.Контрагент = Выборка.Контрагент;
		//	ТекСтрокаВС.Доход = Выборка.НачисленоДохода;
		//	ТекСтрокаВС.Налог = Выборка.УдержВС;
		//Если Объект.ПериодРегистрации >= УчетНДФЛ.ДатаИзмененияСхемыУчетаВС() Тогда	
		//	ТекСтрокаВС.ВидДохода = Справочники.ВидыДоходовНДФЛ.Код42.ОблагаетсяВоеннымСбором2021;
		//Иначе
		//	ТекСтрокаВС.ВидДохода = Справочники.ВидыДоходовНДФЛ.ВоенныйСбор;
		//КонецЕсли;	
		//КонецЕсли;
		
	КонецЦикла; 
	
	
КонецПроцедуры


#КонецОбласти