
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ПутьСохранения = КаталогДокументов();
	Режим = РежимДиалогаВыбораФайла.Сохранение;
	ДиалогСохраненияФайла = Новый ДиалогВыбораФайла(Режим);
	ДиалогСохраненияФайла.ПолноеИмяФайла = СтрШаблон("Вивантаження видаткових накладних від %1", Формат(ТекущаяДата(), "ДФ=dd-MM-yyyy"));
	Фильтр = "Файл XLS (*.XLS)|*.XLS";
	ДиалогСохраненияФайла.Фильтр = Фильтр;
	ДиалогСохраненияФайла.МножественныйВыбор = Ложь;
	ДиалогСохраненияФайла.Заголовок = "Оберіть файл";
	ДиалогСохраненияФайла.Каталог = ПутьСохранения;
	Если ДиалогСохраненияФайла.Выбрать() Тогда

			ДобавитьВXLS(ПараметрКоманды, ДиалогСохраненияФайла.ПолноеИмяФайла);

	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВXLS(ПараметрКоманды, ПолноеИмяФайла)
	
	ТабДок = ПолучитьТаблицуНаСервере(ПараметрКоманды);
	Попытка
		ТабДок.Записать(ПолноеИмяФайла, ТипФайлаТабличногоДокумента.XLS97);
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьТаблицуНаСервере(ПараметрКоманды)
	
	ТабДок = Новый ТабличныйДокумент;
	Макет = Документы.РеализацияТоваровУслуг.ПолучитьМакет("а_МакетВыгрузкиВXLS");
	
	Для Каждого Док Из ПараметрКоманды Цикл
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Ссылка", Док);
		Запрос.Текст = "ВЫБРАТЬ
		               |	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
		               |	РеализацияТоваровУслуг.Дата КАК Дата,
		               |	РеализацияТоваровУслуг.Номер КАК Номер,
		               |	РеализацияТоваровУслуг.ПунктРазгрузки КАК ПунктРазгрузки,
		               |	РеализацияТоваровУслуг.Контрагент.КодПоЕДРПОУ КАК ЕДРПОУКонтрагента,
		               |	РеализацияТоваровУслуг.Организация.КодПоЕДРПОУ КАК ЕДРПОУОрганизации,
		               |	РеализацияТоваровУслуг.Номер КАК НомерДоговора,
		               |	РеализацияТоваровУслуг.Дата КАК ДатаДоговора,
		               |	"""" КАК НомерСчета,
		               |	РеализацияТоваровУслугТовары.Номенклатура.НаименованиеПолное КАК Номенклатура,
		               |	РеализацияТоваровУслугТовары.Коэффициент КАК Коэффициент,
		               |	РеализацияТоваровУслугТовары.Количество КАК Количество,
		               |	РеализацияТоваровУслугТовары.Цена КАК Цена,
		               |	РеализацияТоваровУслугТовары.СтавкаНДС КАК СтавкаНДС,
		               |	РеализацияТоваровУслугТовары.СуммаНДС КАК СуммаНДС,
		               |	РеализацияТоваровУслугТовары.Сумма КАК Сумма,
		               |	РеализацияТоваровУслуг.СуммаВключаетНДС КАК СуммаВключаетНДС
		               |ИЗ
		               |	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		               |		ПО РеализацияТоваровУслугТовары.Ссылка = РеализацияТоваровУслуг.Ссылка
		               |ГДЕ
		               |	РеализацияТоваровУслуг.Ссылка = &Ссылка";
		
		Выборка = Запрос.Выполнить().Выбрать();
		ВыводимШапку = Истина;
		Пока Выборка.Следующий() Цикл
			Если ВыводимШапку Тогда
				ВыводимШапку = Ложь;
				
				Область = Макет.ПолучитьОбласть("Шапка");
				Область.Параметры.Заполнить(Выборка);
				Область.Параметры.Дата = Формат(Область.Параметры.Дата, "ДФ=dd.MM.yyyy");
				ТабДок.Вывести(Область);
			КонецЕсли;
			
		 	Область = Макет.ПолучитьОбласть("Строка");
			ЗаполнитьЗначенияСвойств(Область.Параметры, Выборка);
			СтавкаНДС = УчетНДС.ПолучитьСтавкуНДС(Выборка.СтавкаНДС);
			Область.Параметры.СтавкаНДС = СтавкаНДС;
			Область.Параметры.СуммаНДС = Формат(Выборка.СуммаНДС, "ЧДЦ=2; ЧГ=");
			
			Область.Параметры.Количество = Формат(Область.Параметры.Количество, "ЧДЦ=3; ЧГ=");
			
			Если Выборка.СуммаВключаетНДС Тогда
				Область.Параметры.ЦенаБезНДС = Формат(Выборка.Цена - Выборка.Цена/(100+СтавкаНДС)*СтавкаНДС, "ЧДЦ=2; ЧГ=");
				Область.Параметры.ЦенаСНДС = Формат(Выборка.Цена, "ЧДЦ=2; ЧГ=");
				Область.Параметры.СуммаБезНДС = Формат(Выборка.Сумма - Выборка.СуммаНДС, "ЧДЦ=2; ЧГ=");
				Область.Параметры.СуммаСНДС = Формат(Выборка.Сумма, "ЧДЦ=2; ЧГ=");	
			Иначе
				Область.Параметры.ЦенаБезНДС = Формат(Выборка.Цена, "ЧДЦ=2; ЧГ=");
				Область.Параметры.ЦенаСНДС = Формат(Выборка.Цена + Выборка.Цена * СтавкаНДС / 100, "ЧДЦ=2; ЧГ=");
				Область.Параметры.СуммаБезНДС = Формат(Выборка.Сумма, "ЧДЦ=2; ЧГ=");
				Область.Параметры.СуммаСНДС = Формат(Выборка.Сумма + Выборка.СуммаНДС, "ЧДЦ=2; ЧГ=");				
			КонецЕсли;
			
			ТабДок.Вывести(Область);			
			
		КонецЦикла;
		
	 	Область = Макет.ПолучитьОбласть("Разделитель");
		ТабДок.Вывести(Область);
   		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции
