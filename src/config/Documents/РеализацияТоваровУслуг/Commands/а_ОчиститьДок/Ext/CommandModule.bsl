
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)

	ПоказатьВопрос(Новый ОписаниеОповещения("ПослеЗакрытияВопросаОчисткиДокумента", ЭтотОбъект, ПараметрКоманды),
		НСтр("ru = 'Очистить документы? 
              |Внимание! Также будет очищена история по документам, возврат к версии будет невозможный!'; uk = 'Очистити документи?
              |Увага! Також буде вилучено історію по документам, повернення до версії буде неможливо!'"),
		РежимДиалогаВопрос.ДаНет,,,
		НСтр("ru = 'Очистить?'; uk = 'Очистити?'"),
	);

КонецПроцедуры
	
&НаКлиенте
Процедура ПослеЗакрытияВопросаОчисткиДокумента(Результат, МассивДокументов) Экспорт
	
	Если МассивДокументов = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ОчиститьДокументыНаСервере(МассивДокументов);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьДокументыНаСервере(МассивДокументов)
	
	Для Каждого ДокСсылка Из МассивДокументов Цикл
		Об = ДокСсылка.ПолучитьОбъект();
		
		ЗаполнитьЗначенияСвойств(Об, Документы.РеализацияТоваровУслуг.СоздатьДокумент(),, "Дата, Номер, ВидОперации");
		
		Для Каждого ТЧ ИЗ Метаданные.Документы.РеализацияТоваровУслуг.ТабличныеЧасти Цикл
			Об[ТЧ.Имя].Очистить();	
		КонецЦикла;
		
		Попытка
			Об.Записать(?(Об.Проведен, РежимЗаписиДокумента.ОтменаПроведения, РежимЗаписиДокумента.Запись));
			Об.УстановитьПометкуУдаления(Истина);
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтрШаблон(НСтр("ru = 'Ошибка записи документа %1'; uk = 'Помилка запису документу %1'"), Об.Ссылка)
			);
			ЗаписьЖурналаРегистрации("Очистка РТУ", УровеньЖурналаРегистрации.Ошибка, Об.Метаданные(), Об.Ссылка, ОписаниеОшибки());
		КонецПопытки;
		
		УстановитьПривилегированныйРежим(Истина);
		Набор = РегистрыСведений.ВерсииОбъектов.СоздатьНаборЗаписей();
		Набор.Отбор.Объект.Установить(Об.Ссылка);
		Попытка
			Набор.Записать();
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Ошибка очистки истории по документу'; uk = 'Помилка вилучення історії по документу'"));
			ЗаписьЖурналаРегистрации("Очистка РТУ", УровеньЖурналаРегистрации.Ошибка, Об.Метаданные(), Об.Ссылка, ОписаниеОшибки()); 
		КонецПопытки;
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЦикла;
	
КонецПроцедуры
