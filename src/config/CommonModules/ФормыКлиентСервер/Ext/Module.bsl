#Область Отборы

Процедура ВыделитьИспользуемыеОтборы(ОформленияСтрок) Экспорт
	Для Каждого ТекОформление Из ОформленияСтрок Цикл
		Если ТекОформление.ДанныеСтроки.Использование Тогда
			ТекОформление.ЦветФона = WebЦвета.СветлоЗолотистый;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ПрименитьОтборКТабличнойЧасти(ТабличнаяЧасть,Отборы) Экспорт
	Для Каждого ЭлементОтбора Из Отборы Цикл
		НайденныйОтбор = ТабличнаяЧасть.Отбор.Найти(ЭлементОтбора.Имя);
		Если НЕ НайденныйОтбор = Неопределено Тогда
			ЗаполнитьЗначенияСвойств(НайденныйОтбор,ЭлементОтбора);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ДобавитьЭлементОтбора(ДоступныеПоляОтбора,ОтборыФормы,ТипЭлемента,Имя, Синоним = Неопределено) Экспорт
	МассивТипов 		= Новый Массив;
	МассивТипов.Добавить(ТипЭлемента);
	ПолеОтбора 			= ДоступныеПоляОтбора.Добавить(Имя, ?(Синоним = Неопределено,Имя,Синоним), Новый ОписаниеТипов(МассивТипов));
	ПолеОтбора.Отбор 	= Истина;
	ОтборыФормы.УстановитьДоступныеПоля(ДоступныеПоляОтбора);
	ЭлементОтбораФормы 	= ОтборыФормы.Добавить(Имя);
КонецПроцедуры

Функция УстановитьЗначениеОтбора(ДинамическийСписок, ИмяОтбора, Значение, ВидСравнения = Неопределено, Использование = Истина) Экспорт
	
	КоллекцияОтборов = ДинамическийСписок.Отбор.Элементы;
	
	Если ВидСравнения = Неопределено Тогда
		Если ТипЗнч(Значение) = Тип("Массив") или ТипЗнч(Значение) = Тип("ФиксированныйМассив") Тогда
			ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
		Иначе
			ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		КонецЕсли;
	КонецЕсли;
	
	// Ищем
	ИскомыйОтбор = Неопределено;
	Для Каждого ТекОтбор Из КоллекцияОтборов Цикл
		Если Строка(ТекОтбор.ЛевоеЗначение) = ИмяОтбора Тогда
			ИскомыйОтбор = ТекОтбор;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ИскомыйОтбор = Неопределено Тогда // Если нету
		ИскомыйОтбор = КоллекцияОтборов.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ИскомыйОтбор.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных(ИмяОтбора);
	КонецЕсли;
	// Заполняем
	ИскомыйОтбор.Использование  = Использование;
	ИскомыйОтбор.ВидСравнения   = ВидСравнения;
	Если ТипЗнч(Значение) = Тип("Массив") Тогда
		ИскомыйОтбор.ПравоеЗначение = Новый Массив;
		Для Каждого ТекЗначение Из Значение Цикл
			ИскомыйОтбор.ПравоеЗначение.Добавить(ТекЗначение);
		КонецЦикла;
	Иначе
		ИскомыйОтбор.ПравоеЗначение = Значение;
	КонецЕсли;
	Возврат ИскомыйОтбор;
КонецФункции

Функция УстановитьОтборПоДате(ДинамическийСписок, Дата, Знач ДатаОкончания = Неопределено) Экспорт
	
	КомпоновкаДанныхСервер.УстановитьПараметрДанных(ДинамическийСписок,"НачалоПериода",Дата);
	КомпоновкаДанныхСервер.УстановитьПараметрДанных(ДинамическийСписок,"КонецПериода",КонецДня(?(ДатаОкончания = Неопределено,Дата,ДатаОкончания)));
	
КонецФункции

Процедура ОтключитьОтбор(ДинамическийСписок, ИмяОтбора) Экспорт
	Для Каждого ТекОтбор Из ДинамическийСписок.Отбор.Элементы Цикл
		Если Строка(ТекОтбор.ЛевоеЗначение) = ИмяОтбора Тогда
			ТекОтбор.Использование  = Ложь;
			Возврат;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Функция ЗначениеОтбора(ДинамическийСписок, ИмяОтбора) Экспорт
	
	Для Каждого ТекОтбор Из ДинамическийСписок.Отбор.Элементы Цикл
		
		Если Строка(ТекОтбор.ЛевоеЗначение) = ИмяОтбора Тогда
			
			Возврат ?(ТекОтбор.Использование, ТекОтбор.ПравоеЗначение, Неопределено);
			
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

&НаКлиенте
Функция ЭлементУстановитьОтборы(Элемент,СтрокаСтруктураПолей,ЭлементИсточникЗначенийОтбора) Экспорт
	СтрокаОтборов 			= Новый Массив;
	МассивЗначенийОтбора 	= Новый Массив;
	СтруктураПолей 			= ?(ТипЗнч(СтрокаСтруктураПолей) = Тип("Строка"),Новый Структура(СтрокаСтруктураПолей),СтрокаСтруктураПолей);
	
	Для Каждого ТекИмяОтбора Из СтруктураПолей Цикл
		Если ЗначениеЗаполнено(ТекИмяОтбора.Значение) Тогда
			Ном 			= Найти(ТекИмяОтбора.Значение,".");
			Если Ном = 0 Тогда
				ТекЗначение 	= ЭлементИсточникЗначенийОтбора.ТекущиеДанные[ТекИмяОтбора.Значение];
			Иначе
				ПолеДоТочки 	= Лев(ТекИмяОтбора.Значение,Ном - 1);
				ПолеПослеТочки 	= Сред(ТекИмяОтбора.Значение,Ном + 1);
				ТекЗначение 	= ЭлементИсточникЗначенийОтбора.ТекущиеДанные[ПолеДоТочки];
				ТекЗначение 	= ОбщегоНазначенияСервер.ЗначенияРеквизитовОбъекта(ТекЗначение,ПолеПослеТочки);//Вычислить("ТекЗначение." + ПолеПослеТочки);
			КонецЕсли;
		Иначе
			ТекЗначение = ЭлементИсточникЗначенийОтбора.ТекущиеДанные[ТекИмяОтбора.Ключ];
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТекЗначение) Тогда
			СтрокаОтборов.Добавить("Отбор." + ТекИмяОтбора.Ключ);
			МассивЗначенийОтбора.Добавить(ТекЗначение);
		КонецЕсли;
	КонецЦикла;
	
	Если СтрокаОтборов.Количество() = 0 Тогда // Нет отбора
		Элемент.ПараметрыВыбора = Новый ФиксированныйМассив(Новый Массив);
	Иначе
		ФормыКлиентСервер.УстановитьОтборНаПолеФормыВыбора(Элемент, СтрокаОтборов, МассивЗначенийОтбора);
	КонецЕсли;
	
КонецФункции

// Получает массив из строки элементов, где элементы разделенны запятыми 
// (строка вида: элемент1,элемент2, элемент3,элемент4 ....)
Функция ПолучитьМассивИзСтрокиЗначений(СтрокаЗначений) Экспорт 
	
	МассивЗначений = Новый Массив;
	
	ОбрабатываемаяСтрока = СтрЗаменить(СтрокаЗначений, " ", ""); // уберем пробелы
	
	Поз = Найти(ОбрабатываемаяСтрока, ",");
	
	Пока Поз <> 0 Цикл  
		
	    МассивЗначений.Добавить(Лев(ОбрабатываемаяСтрока, Поз - 1));	
		ОбрабатываемаяСтрока = Прав(ОбрабатываемаяСтрока, СтрДлина(ОбрабатываемаяСтрока) - Поз);
		
		Поз = Найти(ОбрабатываемаяСтрока, ",");
		
	КонецЦикла;	
	
	МассивЗначений.Добавить(ОбрабатываемаяСтрока);
	
	Возврат МассивЗначений;
	
КонецФункции

&НаКлиенте
Процедура УстановитьОтборНаПолеФормыВыбора(Элемент, СтрокаЗначений, МассивЗначений, ПрименятьЕслиНеЗаполненоЗначение = Истина) Экспорт
	
	Если ТипЗнч(СтрокаЗначений) = Тип("Строка") Тогда
		Мас =  ПолучитьМассивИзСтрокиЗначений(СтрокаЗначений);
	Иначе
		Мас = СтрокаЗначений;
	КонецЕсли;
	
	МассивПараметров = Новый Массив;
	
	Если ТипЗнч(МассивЗначений) <> Тип("Массив") Тогда
		МассивПараметров.Добавить(Новый ПараметрВыбора(Мас[0], МассивЗначений));
	Иначе
		Для Индекс = 0 По Мас.Количество() - 1 Цикл 
			
			Если МассивЗначений.Количество() - 1 >= Индекс И (ЗначениеЗаполнено(МассивЗначений[Индекс]) Или ПрименятьЕслиНеЗаполненоЗначение) Тогда 
				
				МассивПараметров.Добавить(Новый ПараметрВыбора(Мас[Индекс], МассивЗначений[Индекс]));
				
			КонецЕсли;	
			
		КонецЦикла;
	КонецЕсли;
	
	Если МассивПараметров.Количество() > 0 Тогда 
		
		Элемент.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
		
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

// Устанавливает ограничения в динамическогм списке на отборы, сортировки и группировку на все поля списка
// для ускорения поиска по строке поиска и уменьшает количество ошибочных настроек пользователя
// убирает из ограничений поля указанные в отборах условного оформления, т.к. это ломает оформление
// Параметры:
//  Список           - ДинамическийСписок - Динамический список на который устанавливается отбор
//                 
//  РазрешенныеПоля  - Массив - состоящий из строк-имен полей по которым не устанавливаются ограничения
//
//
Процедура УстановитьОграничениеДинамическогоСписка(Список, РазрешенныеПоля) Экспорт

	Для Каждого ЭлементНастроек Из Список.КомпоновщикНастроек.Настройки.УсловноеОформление.Элементы Цикл
		Для Каждого ЭлементОтбора Из ЭлементНастроек.Отбор.Элементы Цикл
			РазрешенныеПоля.Добавить(Строка(ЭлементОтбора.ЛевоеЗначение));
		КонецЦикла;
	КонецЦикла;
	МассивПолей = Новый Массив;
	Для Каждого ЭлементСписка Из Список.КомпоновщикНастроек.Настройки.ДоступныеПоляВыбора.Элементы Цикл
		Если РазрешенныеПоля.Найти(Строка(ЭлементСписка.Поле)) = Неопределено ИЛИ ЭлементСписка.Папка Тогда
			МассивПолей.Добавить(Строка(ЭлементСписка.Поле));
		КонецЕсли;
	КонецЦикла;
	
	Список.УстановитьОграниченияИспользованияВГруппировке(МассивПолей);
	Список.УстановитьОграниченияИспользованияВОтборе(МассивПолей);
	Список.УстановитьОграниченияИспользованияВПорядке(МассивПолей);
	
КонецПроцедуры // УстановитьОграничениеДинамическогоСписка()
        